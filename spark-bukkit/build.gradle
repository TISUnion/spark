plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

dependencies {
    implementation project(':spark-common')
    implementation('me.lucko:adventure-platform-bukkit:4.9.4') {
        exclude(module: 'adventure-api')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
        exclude(module: 'adventure-text-serializer-gson')
        exclude(module: 'adventure-text-serializer-legacy')
    }
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    
    // placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') {
        exclude(module: 'MVdWUpdater')
    }
}

repositories {
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "spark-${project.pluginVersion}-bukkit.jar"

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

plugins {
    id 'com.google.protobuf' version '0.8.16'
    id 'maven-publish'
}

license {
    exclude '**/sampler/async/jfr/**'
}

dependencies {
    api project(':spark-api')
    implementation 'com.github.jvm-profiling-tools:async-profiler:v2.8.3'
    implementation 'org.ow2.asm:asm:9.1'
    implementation 'com.google.protobuf:protobuf-javalite:3.15.6'
    implementation 'net.bytebuddy:byte-buddy-agent:1.11.0'
    api('net.kyori:adventure-api:4.11.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
    }
    api('net.kyori:adventure-text-serializer-gson:4.11.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
        exclude(module: 'gson')
    }
    api('net.kyori:adventure-text-serializer-legacy:4.11.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
    }
    implementation('net.kyori:adventure-text-feature-pagination:4.0.0-SNAPSHOT') {
        exclude(module: 'adventure-api')
    }
    compileOnly 'com.google.code.gson:gson:2.7'
    compileOnly 'com.google.guava:guava:19.0'
    compileOnly 'org.checkerframework:checker-qual:3.8.0'
}

protobuf {
    protoc {
        if (System.getProperty("os.name") == "Mac OS X" && System.getProperty("os.arch") == "aarch64") {
            path = '/opt/homebrew/bin/protoc'
        } else {
            artifact = 'com.google.protobuf:protoc:3.15.6'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

// copied from spark-api
publishing {
    //repositories {
    //    maven {
    //        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    //        credentials {
    //            username = sonatypeUsername
    //            password = sonatypePassword
    //        }
    //    }
    //}
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'spark core'
                description = 'spark is a performance profiling plugin/mod for Minecraft clients, servers and proxies.'
                url = 'https://spark.lucko.me/'
            }
        }
    }
}
